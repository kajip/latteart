group 'jp.co.biglobe'
version '0.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8

// lombok 等のため、provided 定義を追加
configurations {
    provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}


/* == 'application' plugin Setting == */

mainClassName = "jp.co.biglobe.isp.monitor.Main"

// 起動スクリプトに JAVA_HOME/lib/tools.jar を追加
startScripts {
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile    = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('CLASSPATH=%APP_HOME%\\lib', 'CLASSPATH=%JAVA_HOME%\\lib\\tools.jar;%APP_HOME%\\lib')
        unixScriptFile.text  = unixScriptFile.text.replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$JAVA_HOME/lib/tools.jar:$APP_HOME/lib')
    }
}

run {
    // クラスパスに tools.jar を追加
    classpath += configurations.provided

    // run タスクにコマンド引数を渡す設定
    // 使い方： gradle run -Pargs="-c http://localhost:8080/config.json 1234"
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}


repositories {
    mavenCentral()
}

import javax.tools.ToolProvider

dependencies {

    provided "org.projectlombok:lombok:1.16.6"

    // Attach API で利用. $JAVA_HOME/lib/tools.jar の読込
    provided (files(((URLClassLoader) ToolProvider.getSystemToolClassLoader()).getURLs()))

    compile "org.slf4j:slf4j-api:1.7.13"
    runtime "org.slf4j:slf4j-simple:1.7.13"

    compile "commons-cli:commons-cli:1.3.1"

    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.3'

    testCompile "junit:junit:4.11"
}
